%------------------ Задание ------------------
%Найдите число d < 1000, для которого 1/d
%содержит самый длинный период
%--------------- Конец задания ---------------


%------------------ Теория ------------------
%Любая правильная или неправильная дробь представима в виде c,r(p), 
%где с - целая часть, r - предпериод, p - период дроби.

%Предпериод. Длина = максимальной степени чисел 
%вида 2^p и 5^q, которые делят знаменатель.

%Период. Длина = количеству разрядов числа, составленных из цифры 9, 
%делящегося на число оставшееся от преобразований с предпериодом
%Но удобнее каждый раз делать как деление по модулю
%Если остаток != 0, то *10+9 и снова смотрит делится ли оно на d без остатка 
%--------------- Конец теории ---------------


%-------------------------
% максимум из двух чисел 

maxim(X,Y,X):- 
  X >= Y,
  !.
  
maxim(_,Y,Y).

% модификация, позволяет запоминать соотвествующее
% дополнительное число
maxim(X,Y,X,Z,_,Z):- 
  X >= Y,
  !.
  
maxim(_,Y,Y,_,Q,Q).
%-------------------------

%-------------------------
%для расчёта длины предпериода дроби

%Если не делится на 2 или 5, то конец
pred_period(D,0,Five_or_two, D) :-
   Not_Zero is D mod Five_or_two,
   Not_Zero \= 0, 
   !.

%Иначе продолжаем накапливать степень
pred_period(D, How_many, Five_or_two, New_D):-
   D1 is D div Five_or_two,
   pred_period(D1, How_many1, Five_or_two, New_D),
   How_many is How_many1+1.

%-------------------------

%-------------------------
%для расчёта длины периода дроби

%Если у нас число полностью представленно как степени 2 и 5,
%то и периода нет. -1, чтобы в find_length занулить.
period(1, -1, _, _).

%Делаем последовательное деление по модулю
%Пока не делется полностью, продолжаем перебор и 
%увеличение счётчика длины
period(D, How_many, Ostatok, Same_Ostatok) :-
  Not_Zero is Ostatok mod D,
  Not_Zero \= 0,

  Ostatok1 is Not_Zero,
  Ostatok2 is Ostatok1*10,
  Ostatok3 is Ostatok2+9,

  period(D, How_many1, Ostatok3, Same_Ostatok),
  How_many is How_many1+1.

%Когда наконец оно разделилось, можно и отдохнуть, и прекратить поиск
period(D, 0, Ostatok, Ostatok) :- !.

%-------------------------

%-------------------------
%Поиск длины периода для текущего числа

%У единицы вообще всё по нулям....
find_length(1,0) :- !.

%раскладываем число по степеням 2 и 5, оставшееся от деления число
%посылаем для пиоска длины периода
find_length(D, Length_of_period):-
  pred_period(D, Two, 2, New_D),
  pred_period(New_D, Five, 5, New_D1),
  
  %maxim(Two, Five, Length), %длина предпериода (опционально)
  %write(Length),
  
  period(New_D1, How_many, 9, Same_Ostatok),
  Length_of_period is How_many+1,
  !.
%-------------------------


%-------------------------
%Поиск максимальной длины периода от начала (D) промежутка 
%и до конца (Range)

%Основной предикат, из которого вызываем главный поиск
%Вспомогательные аргументы для хранения значений нулевые
find_period(D,Range,Max,Number):- find_period(D,Range,0,Max,0, Number).

%Если вышли за пределы промежутка, то заканчиваем поиск.
find_period(D,Range,Max1,Max,Number1,Number):-
   D >= Range,
   Max is Max1,
   Number is Number1,
   !.

%Ищем длину периода каждого числа, сравниваем с максимальным
%на данный момент и шагаем дальше   
find_period(D,Range,Max1,Max,Number1,Number):-
  find_length(D, Len),
  maxim(Len, Max1, Max2, D, Number1, Number2),
  D1 is D+1,
  find_period(D1,Range,Max2,Max,Number2,Number).
%-------------------------

 
%-------------------------  
%Мейн
main :-
  find_period(1,1000, Max, Number),
  write("Number with maxim period: "),
  writeln(Number),
  write("Maximum period: "),
  write(Max),
  !.
%-------------------------   
  
  